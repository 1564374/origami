/**
 * Testing Parsing Expression Files
 * Author: Kimio Kuramitsu
 */

Start = { 
	$(TestCase)* 
} !.

TestCase = 
	'.'      Any         /* .a */
	/ 'a'      Char      /* aa */
	/ '['      Class ']' /* [1] */
	/          Seq       /* あ */
	/ '+'      Many      /* +123 */
	/ '?'      Option    /* ?-12 */
	/ '&'      And       /* &A */
	/ '!'      Not       /* !a */
	/ 'on '    On        /* on A */
	/ 'off '   Off       /* on a */
//	/ 'match ' Match     /* match 1x1 */
	
Any = { . #Any }
Char = { 'a' #Char }
Class = { DIGIT #Class }
Seq = { 'あ' #Seq }
Many = { NUM #Many }
Option = { SIGNED #Option }
And = { &UPPER . #Not }
Not = { !UPPER . #Not }
On  = { <on upper ALPHA> #On }
Off  = { <on !upper ALPHA> #Off }
Match  = { <block <symbol DIGIT> . <match DIGIT> > #Match }

CASE  = !''
DIGIT = [0-9]
NUM = DIGIT+
SIGNED = '-'? NUM

UPPER = [A-Z]
LOWER = [a-z]
ALPHA = <if upper> UPPER / <if !upper> LOWER

