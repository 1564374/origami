example Expr 2
example Expr sin
example Expr sin(2)
example Expr sin()
example Expr sin(2,4)
example Expr 2 + 4
example Expr 2 + sin( 2 + 4 )
example Expr x = 3 * func(3)
example Input def add(right , left) right + left
example Input main 1+2


File = Input

Input = { $(Stat)* #Source }

Stat =
  MainFunc
  / FuncDecl
  / ExternFunc

MainFunc = "main" Expr

FuncDecl =  {
    "def"
    $name(Identifier)
    "("  $param(Params) ")"
    $body(Expr)
    #FuncDecl
}

ExternFunc = {
    "extern"
    $name(Identifier)
    "("  $param(Params) ")"
    #ExternFunc
}

Params = {
    ( $(Param) ("," $(Param) )*)?
}

Param = {
    $name(Identifier)
    #LetDecl
}

Expr = 
    MonoExpr
    {$left _Binop $right(MonoExpr) }*

_Binop = 
         "+"  #AddExpr
       / "*" #MulExpr
       
MonoExpr = 
    CallFunc
    / Identifier
    / DoubleLiteral
    / IntegerLiteral

CallFunc = {
    $name(Identifier)
    "("  $param(Arguments) ")"
    #ApplyExpr
}

Arguments = {
    ( $(MonoExpr) ( "," $(MonoExpr) )* )?
}

DoubleLiteral = { DOUBLE #DoubleExpr} _
IntegerLiteral = { INTEGER #IntExpr} _
Identifier = { NAME #NameExpr} _ 

DOUBLE = 
    FRACTION EXPONENT?
    / DECIMAL EXPONENT

INTEGER = DECIMAL
DIGITS = DIGIT+
DIGIT = [0-9]
DECIMAL =
    '0'
    / !'0' DIGITS
FRACTION = 
    '.' DIGITS
    / DIGITS '.' DIGITS?
EXPONENT = [eE] [+/-]? DIGITS

NAME = !DIGIT !KEYWORD W+

W = [a-zA-Z0-9]

/* Whitespace */
_ = [ \n\r\t\u000B\u000C]*

/* Operator */
"+" = '+'_
"*" = '*'_
"(" = '('_
")" = ')'_
"," = ','_

/* Keyword */
KEYWORD  
    = "def"
    / "extern"
    / "main"

"main" = 'main' !W _
"def" = 'def' !W _
"extern" = 'extern' !W _
